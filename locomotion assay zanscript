ACTION MAIN

INCLUDE zsys

DEFINE PRETRIAL_TIME 10 #THE TIME IN SECONDS BEFORE THE START OF DATA LOGING/RECORDING
DEFINE NUM_BLOCK 1 #A LOOP COMMAND 
DEFINE NUM_SAMPLES 600 #THE NUMBER OF SAMPLES, WHICH IS TIME IN SECONDS THAT THE PROGRAMME WILL RUN FOR, WORKS IN CONJUNCTION WITH SAMPLE_TIME
DEFINE NUM_ITI 0 #INTER-TRIAL TIME, OFF HERE
DEFINE SAMPLE_TIME 1 #DEFINES SAMPLE AS 1=1 SECOND

SET(TARGET_SIZE,4) #SET TARGET ANIMAL SIZE, IN MM 
SET(DETECTOR_THRESHOLD,6) #THRESHOLD TO DIFFERENTIATE THE ANIMAL FROM THE BACKGROUND. A HIGHER NUMBER IS MORE TOLERANT TO NOISE

#SET(TILE_SIZE,5)
#SET(DETECTOR_THRESHOLD,6)
#SET(SEARCH_DISTANCE,25)
#SET(SEARCH_STEP,5)
#SET(FILTER_RADIUS,4)

SET(AUTOREF_MODE,1) #TURNS ON AUTOREFERENCE, WHICH REFERERS TO A ‘CLEAN’ IMAGE OF THE AREANA FOR OPTIMAL TRACKING
SET(AUTOREF_TIMEOUT,10) #SETS THE MAXIMUM AMOUNT OF TIME FOR THE AUTOREFERENCE PROCEEDURE


SET(THERMOSTAT,24) #SETS TEMPERATURE IN DEGREES CENTIGRAGE

DEFINE X_LOGDATA_TRACKS 799 #ENSURES THE TRACK DATA IS ‘LOGGED’ AND SAVED
DEFINE X_DRAWTRACKS 30011 #SETTING TO ALLOW FOR THE TRACK DRAWING TO BE DISPLAYED. WORKS IN CORRESPONDENCE WITH SET(X,DRAWTRACKS,1) IN THE ACTION MAIN BELOW



DEFINE X_OVERLAY_TEXT 30018 #SETTINGS FOR WRITING TEXT OVER THE VIDEO
DEFINE X_OVERLAY_CLEAR 30019 #SETTINGS TO CLEAR TEXT OVER THE VIDEO

ACTION MAIN

	SET(COUNTER1,COUNTER_ZERO)
	SET(COUNTER2,COUNTER_ZERO)

	LOAD(ARENAS,"a8.bmp") #LOADS THE IMAGE FILE REPRESENTING THE 	ARENA, THIS CAN BE CHANGED IN THE ASSETS DIRECTORY

	LOGCREATE("TEXT:TIME|TEXT:TEMPERATURE|TEXT:CONDITION|TEXT:TIME_BIN|TEXT:BLOCK") #THE DATA OUTPUT TITLES
	LOGAPPEND("TEXT:A1|TEXT:A2|TEXT:A3|TEXT:A4|TEXT:A5|TEXT:A6")
       LOGAPPEND("TEXT:A7|TEXT:A8") #CORRESOPNDS WITH THE NUMBER OF ARENAS
	
    LOGRUN()

	WAIT(PRETRIAL_TIME) #TIME BEFORE THE RUN STARTS

	AUTOREFERENCE() #REFERS TO THE IMAGE OF THE ARENA WITHOUT THE ORGANISM IN IT. THE REFERENCE IMAGE IS STORED AS A CLEAN BACKGROUND

	SET(X_DRAWTRACKS,1) #TURNS ON ON-SCREEN TRACKING TRACE
    
    INVOKE(BLOCK,NUM_BLOCK) #TURNS ON THE BLOCK ACTION ONCE. THIS HAD THE POTENTIAL TO CREATE A SAMPLING LOOP
        
COMPLETE

ACTION BLOCK #THIS ACTION BLOCK HAS THE POTENTIAL TO CREATE A LOOP. NOT USED IN THIS ASSAY

	INVOKE(LIGHTS_ON) #TURNS THE LIGHTS ON
	SET(COUNTER1,COUNTER_ZERO) #COUNTERS, FOR LOOPS, OFF
	SET(COUNTER2,COUNTER_INC) #COUNTERS, FOR LOOPS, OFF
    
    VIDEO(NUM_SAMPLES,"Xen_distance_moved_assay") #NUMBER OF SECONDS THE VIDEO WILL RECORD FOR AND THE TITLE IT WILL BE SAVED AS
    #VIDEOSKIP(29)
	
   INVOKE(SAMPLE,NUM_SAMPLES) #WILL RUN THE SAMPLE ACTION REPEATEDLY ONCE PER SECOND, 600 TIMES
    
    VIDEOSTOP

COMPLETE

ACTION LIGHTS_ON

SET(GPO6,0) #CHANGE TO (GP06,1) TO TURN ON. GP06 CONTROLS RED LIGHT
SET(GPO7,0) #CHANGE TO (GP07,1) TO TURN ON. GP07 CONTROLS GREEN LIGHT
SET(GPO8,0) #CHANGE TO (GP08,1) TO TURN ON. GP08 CONTROLS BLUE LIGHT


COMPLETE

ACTION SAMPLE

	SET(COUNTER1,COUNTER_INC) #COUNTS EACH SAMPLE (THE TIME IN SECONDS)

	LOGDATA(DATA_SNAPSHOT,"begin") #CAPTURES THE POSITION AT THE START AND END OF THE ONE SECOND SAMPLE
	WAIT(SAMPLE_TIME)

	LOGDATA(DATA_SNAPSHOT,"end") #LOGS EXTRACTED TRACKING DATA, DISTANCE MOVED, VELOCITY AND POSITION CHANGE
	LOGDATA(DATA_SELECT,"begin")
	LOGDATA(DATA_DELTA,"end")

	LOGCREATE("RUNTIME|TEMPERATURE1|TEXT:REST|COUNTER1|COUNTER2|ARENA_DISTANCES:*") #CREATES A DATA RECORD FOR RUNTIME, TEMPERATURE, DISTANCE MOVED PER ARENA                                        
	LOGRUN() #WRITES THE DATA RECORD

COMPLETE
